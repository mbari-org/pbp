## Some gizo related recipes were moved from the main justfile to this one
## to keep the main justfile less confusing to external users.
## For now this is done just to faciliate reference, but this file could
## be removed in the future.
## Also, these recipes may need adjustmentss as they haven't been used in a while.

_list:
    @just --list --unsorted

# Run main (on gizo)
main-gizo date="20220902" output_dir="/mnt/PAM_Analysis/pypam-space/test_output/daily":
    PYTHONPATH=. poetry run python pbp/main_hmb_generator.py \
                 --json-base-dir=json \
                 --date={{date}} \
                 --voltage-multiplier=3 \
                 --sensitivity-uri=misc/icListen1689_sensitivity_hms256kHz.nc \
                 --subset-to 10 100000 \
                 --audio-path-map-prefix="s3://pacific-sound-256khz-2022~file:///mnt/PAM_Archive/2022" \
                 --output-dir={{output_dir}}

# Run main (on gizo) with some initial test jsons
main-gizo-test *more_args="":
    PYTHONPATH=. poetry run python pbp/main_hmb_generator.py \
                 --json-base-dir=tests/json \
                 --date=20220902 \
                 --voltage-multiplier=3 \
                 --sensitivity-uri=misc/icListen1689_sensitivity_hms256kHz.nc \
                 --subset-to 10 100000 \
                 --audio-base-dir=tests/wav \
                 --audio-path-map-prefix="s3://pacific-sound-256khz-2022~file:///mnt/PAM_Archive/2022" \
                 --output-dir=/mnt/PAM_Analysis/pypam-space/test_output/daily \
                 {{more_args}}

# Run multiple days (on gizo)
main-gizo-multiple-days year month *days="":
    #!/usr/bin/env bash
    source virtenv/bin/activate
    set -ue
    output_dir="/mnt/PAM_Analysis/pypam-space/test_output/daily"
    echo "Running: year={{year}} month={{month}} days={{days}}"
    export PYTHONPATH=.
    for day in {{days}}; do
      date=$(printf "%04d%02d%02d" {{year}} {{month}} "$day")
      base="$output_dir/milli_psd_$date"
      out="$base.out"
      echo "running: day=$day output_dir=$output_dir"
      poetry run python pbp/main_hmb_generator.py \
             --json-base-dir=json \
             --date="$date" \
             --voltage-multiplier=3 \
             --sensitivity-uri=misc/icListen1689_sensitivity_hms256kHz.nc \
             --subset-to 10 100000 \
             --audio-path-map-prefix="s3://pacific-sound-256khz-{{year}}~file:///mnt/PAM_Archive/{{year}}" \
             --output-dir="$output_dir" \
             > "$out" 2>&1 &
    done
    wait

# https://github.com/mbari-org/pbp/issues/89
[no-cd]
hmb_gen_for_direct_file:
    PYTHONPATH=. poetry run python pbp/main_hmb_generator.py   \
       --input-file         local/downloads/MARS_20250914_122000.wav \
       --time-resolution    1                                  \
       --voltage-multiplier 3                                  \
       --sensitivity-uri    local/hmb_direct_file_tests/MARS_icListen6299_calibration_April2025.nc        \
       --global-attrs       local/hmb_direct_file_tests/MARSrealtime_HMB_GlobalAttributes_April2025.yaml  \
       --variable-attrs     local/hmb_direct_file_tests/MARSrealtime_HMB_VariableAttributes.yaml          \
       --output-dir         local/hmb_direct_file_tests
